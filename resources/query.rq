PREFIX cube: <https://cube.link/>
PREFIX time: <http://www.w3.org/2006/time#>

SELECT ?year ?month ?producerPrice ?consumerPrice
WHERE {
  {
    SELECT ?month ?year ?producerPrice
    WHERE {
      GRAPH <https://lindas.admin.ch/foag/agricultural-market-data> {
        FILTER (?product in ( <https://agriculture.ld.admin.ch/foag/product/243> ) )
        FILTER (?productOrigin in ( <https://agriculture.ld.admin.ch/foag/product-origin/1> ) )
        <https://agriculture.ld.admin.ch/foag/cube/MilkDairyProducts/Production_Price_Month> cube:observationSet ?observationSet .
        ?observationSet cube:observation ?observation .
        ?observation <https://agriculture.ld.admin.ch/foag/dimension/product> ?product .
        ?observation <https://agriculture.ld.admin.ch/foag/dimension/product-origin> ?productOrigin .
        ?observation <https://agriculture.ld.admin.ch/foag/measure/price> ?producerPrice .
        ?observation <https://agriculture.ld.admin.ch/foag/dimension/date> ?date .
      }
      ?date time:year ?year.
      ?date time:month ?month.
    }
  }
  OPTIONAL {
    SELECT ?consumerPrice ?year ?month
    WHERE {
      GRAPH <https://lindas.admin.ch/foag/agricultural-market-data> {
        FILTER (?product in ( <https://agriculture.ld.admin.ch/foag/product/239> ) ) # Select UHT milk
        FILTER (?productionSystem in ( <https://agriculture.ld.admin.ch/foag/production-system/3> ) ) # Only select conventional milk
        <https://agriculture.ld.admin.ch/foag/cube/MilkDairyProducts/Consumption_Price_Month> cube:observationSet ?observationSet .
        ?observationSet cube:observation ?observation .
        ?observation <https://agriculture.ld.admin.ch/foag/dimension/product> ?product .
        ?observation <https://agriculture.ld.admin.ch/foag/dimension/production-system> ?productionSystem .
        ?observation <https://agriculture.ld.admin.ch/foag/measure/price> ?consumerPrice .
        ?observation <https://agriculture.ld.admin.ch/foag/dimension/date> ?date .
      }
      ?date time:year ?year.
      ?date time:month ?month.
    }
  }
}
