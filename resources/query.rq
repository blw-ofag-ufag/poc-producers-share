PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <http://schema.org/>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX dimension: <https://agriculture.ld.admin.ch/foag/dimension/>
PREFIX measure: <https://agriculture.ld.admin.ch/foag/measure/>
SELECT ?date ?producerPrice ?consumerPrice
WHERE {
  ?consumerPriceObservation dimension:product/schema:name "Vollmilch UHT"@de ;
    dimension:production-system/schema:name "Konventionell"@de ;
    dimension:value-chain/schema:name "Konsum"@de ;
    measure:price ?consumerPrice ;
    dimension:date ?period .
  ?producerPriceObservation dimension:product/schema:name "Molkereimilch (konv.)"@de ;
    dimension:production-system/schema:name "Konventionell"@de ;
    dimension:value-chain/schema:name "Produktion"@de ;
    dimension:product-origin/schema:name "Schweiz"@de ;
    measure:price ?producerPriceRaw ;
    dimension:date ?period .
  ?period time:hasBeginning/time:inXSDDateTimeStamp ?dateTime ;
    time:hasDurationDescription/schema:name "1 month"@en .
  BIND( xsd:date( ?dateTime ) AS ?date )
  BIND( ?producerPriceRaw * 0.01 * 1.03 AS ?producerPrice) # convert units for the producer price from 0.01 CHF/kg to 0.01 CHF/L
  FILTER ( ?date > "2010-12-31"^^xsd:date )
}
ORDER BY ?date
